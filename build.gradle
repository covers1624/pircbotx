plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'net.covers1624'
archivesBaseName = 'pircbotx'
version '2.3'

version = "$version." + (System.getenv("BUILD_NUMBER") ?: "1")
println "Starting build of ${archivesBaseName}, Version: ${version}"

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

configurations {
    shadow
    implementation.extendsFrom shadow
}

dependencies {
    def lombok = 'org.projectlombok:lombok:1.18.12'
    implementation lombok
    annotationProcessor lombok
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    shadow 'org.slf4j:slf4j-api:1.7.30'
    shadow('org.apache.logging.log4j:log4j-slf4j-impl:2.11.1') {
        transitive false
    }
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'com.google.guava:guava:21.0'

    testAnnotationProcessor lombok
    testImplementation 'org.testng:testng:6.9.10'
    testImplementation 'org.mockito:mockito-core:2.0.40-beta'
    testImplementation 'ch.qos.logback:logback-classic:1.1.3'
}

def commonManifest = {
    attributes 'Specification-Title': archivesBaseName
    attributes 'Specification-Vendor': 'covers1624'
    attributes 'Specification-Version': "2.3"
    attributes 'Implementation-Title': archivesBaseName
    attributes 'Implementation-Vendor': 'covers1624'
    attributes 'Implementation-Version': version
    attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
}

jar {
    manifest commonManifest
}

shadowJar {
    classifier = "altlog"
    configurations = [project.configurations.shadow]
    manifest commonManifest

    //Don't include these, we want to override all this functionality.
    exclude 'org/slf4j/impl/Static*.class'

    //Relocate slf4j
    relocate 'org.slf4j', 'net.covers1624.repack.org.slf4j'
    relocate 'org.apache.logging.slf4j', 'net.covers1624.repack.org.apache.logging.slf4j'
}
build.dependsOn shadowJar

task srcJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://maven-upload.covers1624.net/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        pircbotx(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components['java']
            artifact srcJar
            artifact shadowJar
        }
    }
}
